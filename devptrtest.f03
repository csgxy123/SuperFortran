program devtest

   use devptr
   implicit none
   real, device, target, dimension(4)   :: a_dev
   integer, device  :: i
   real :: result(20)

   a_dev = (/ 1.0, 2.0, 3.0, 4.0 /)

   ! Pointer assignment to device array declared on host,
   ! passed as argument. First four result elements.
   arg_dev_ptr => a_dev
   call test<<<1, 1>>>(arg_dev_ptr)
   result(1:4) = mod_res_arr

   !$cuf kernel do <<<*, *>>>
   do i = 1, 4
       mod_dev_arr(i) = arg_dev_ptr(i) + 4.0
       a_dev(i)       = arg_dev_ptr(i) + 8.0
   enddo

   ! Pointer assignment to module array, argument nullified
   ! Second four result elements
   mod_dev_ptr => mod_dev_arr
   arg_dev_ptr => null()
   call test<<<1, 1>>>(arg_dev_ptr)
   result(5:8) = mod_res_arr

   ! Pointer assignment to updated device array, now associated
   ! Third four result elements
   arg_dev_ptr => a_dev
   call test<<<1, 1>>>(arg_dev_ptr)
   result(9:12) = mod_res_arr

   !$cuf kernel do <<<*, *>>>
   do i = 1, 4
       mod_dev_arr(i) = 25.0 - mod_dev_ptr(i)
       a_dev(i)       = 25.0 - arg_dev_ptr(i)
   enddo
   
   ! Non-contiguous pointer assignment to updated device array
   ! Fourth four element elements
   arg_dev_ptr => a_dev(4:1:-1)
   call test<<<1, 1>>>(arg_dev_ptr)
   result(13:16) = mod_res_arr

   ! Non-contiguous pointer assignment to updated module array
   ! Last four elements of the result
   nullify(arg_dev_ptr)
   mod_dev_ptr => mod_dev_arr(4:1:-1)
   call test<<<1, 1>>>(arg_dev_ptr)
   result(17:20) = mod_res_arr

   print *, all(result == (/(real(i), i = 1, 20)/))

end program devtest
